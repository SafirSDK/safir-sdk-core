# Maintainer: DonOregano <lars@foldspace.nu>

pkgname=safir_sdk_core-git
pkgver=logan_r2459.24646b8
pkgrel=1
pkgdesc="Safir SDK Core is a middleware and platform for creation of distributed soft real-time systems. It is Scalable, Reliable, Portable, and last but not least, it is Open!"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.safirsdkcore.com"
license=('GPL3')
groups=()
depends=('boost' 'unixodbc' 'qt5-base' 'cmake' 'python' 'mono' 'java-environment>=6' 'google-breakpad-svn')
makedepends=('git' 'subversion' 'doxygen' 'graphviz')
install=install
source=("$pkgname"::'git+https://github.com/SafirSDK/safir_sdk_core.git#branch=logan'
        'safir-sdk-core.tmpfiles')
sha1sums=('SKIP'
          'efdadf20b5fe5c5bbc84f131cdabef8a65c2e838')
backup=('etc/safir_sdk_core/typesystem.ini' 'etc/safir_sdk_core/logging.ini' 'etc/safir_sdk_core/locations.ini')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "logan_r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo .

  make
}

check() {
  cd "$srcdir/$pkgname"

  make test
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir/" install

  #Create directories
  install -d -m 775 $pkgdir/var/log/safir_sdk_core
  install -d -m 775 $pkgdir/var/lib/safir_sdk_core
  install -Dm644 "$srcdir/safir-sdk-core.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/safir-sdk-core.conf"

  # install assemblies into Mono's GAC
  for file in $pkgdir/usr/lib/safir_sdk_core/*.dll
  do
    gacutil -i $file -root "$pkgdir/usr/lib"
  done

  #install configuration in /etc/safir_sdk_core
  install -d -m 755 $pkgdir/etc/safir_sdk_core
  install -m 644 -t $pkgdir/etc/safir_sdk_core $pkgdir/usr/share/doc/safir_sdk_core/example_configuration/*.ini
}

